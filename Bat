@echo off
chcp 65001 >nul
setlocal EnableDelayedExpansion

:main_menu
cls
echo  ____         _                      ____          _   
echo ^|  _ \  _ __ (_) _ __ ___    ___    ^| __ )   __ _ ^| ^|_ 
echo ^| ^|_) ^|^| '__^|^| ^|^| '_ ` _ \  / _ \   ^|  _ \  / _` ^|^| __^|
echo ^|  __/ ^| ^|   ^| ^|^| ^| ^| ^| ^| ^|^| (_) ^|_ ^| ^|_) ^|^| (_^| ^|^| ^|_ 
echo ^|_^|    ^|_^|   ^|_^|^|_^| ^|_^| ^|_^| \___/(_)^|____/  \__,_^| \__^|
echo.
echo 1. CFG By TwizzleFuzz
echo 2. CFG By sswlix1
echo 3. CFG By Fomelgaut
echo 4. Exit
echo.
set /p CHOICE="Выберите конфиг, который хотите получить: "
if not defined CHOICE (
    echo Неверный ввод.
    pause
    goto :main_menu
)
if "%CHOICE%"=="4" exit /b 0
if "%CHOICE%"=="1" (
    set "CFG_URL=https://raw.githubusercontent.com/ovsepantamerlan36/BatCFG/main/TwizzleFuzz"
    goto :slot_menu
)
if "%CHOICE%"=="2" (
    set "CFG_URL=https://raw.githubusercontent.com/ovsepantamerlan36/BatCFG/main/sswlix1"
    goto :slot_menu
)
if "%CHOICE%"=="3" (
    set "CFG_URL=https://raw.githubusercontent.com/ovsepantamerlan36/BatCFG/main/Fomelgaut"
    goto :slot_menu
)
echo Неверный выбор. Выберите 1, 2, 3 или 4.
pause
goto :main_menu

:slot_menu
cls
echo  ____         _                      ____          _   
echo ^|  _ \  _ __ (_) _ __ ___    ___    ^| __ )   __ _ ^| ^|_ 
echo ^| ^|_) ^|^| '__^|^| ^|^| '_ ` _ \  / _ \   ^|  _ \  / _` ^|^| __^|
echo ^|  __/ ^| ^|   ^| ^|^| ^| ^| ^| ^| ^|^| (_) ^|_ ^| ^|_) ^|^| (_^| ^|^| ^|_ 
echo ^|_^|    ^|_^|   ^|_^|^|_^| ^|_^| ^|_^| \___/(_)^|____/  \__,_^| \__^|
echo.
echo 6 слот - выход в главное меню
set /p SLOT="Выберите слот для конфига: "
if not defined SLOT (
    echo Неверный ввод.
    pause
    goto :slot_menu
)
if "%SLOT%"=="6" goto :main_menu
echo %SLOT%| findstr /r "^[1-5]$" >nul
if errorlevel 1 (
    echo Не может быть слота больше 5 так как он не поддерживается
    pause
    goto :slot_menu
)

for %%D in (C D E F G H I J K L M N O P Q R S T U V W X Y Z) do (
    if exist "%%D:\SteamLibrary\steamapps\common\Counter-Strike Global Offensive\game\bin\win64\" (
        set "TARGET_DIR=%%D:\SteamLibrary\steamapps\common\Counter-Strike Global Offensive\game\bin\win64"
        goto :found
    )
)
echo Папка SteamLibrary не найдена.
pause
goto :slot_menu

:found
set "FILE=pslot%SLOT%"
if exist "%TARGET_DIR%\%FILE%" (
    set /p CONFIRM="Вы точно хотите сохранить в слот %SLOT%? (Да/Нет): "
    if /i "!CONFIRM!"=="Нет" (
        echo Отменено.
        pause
        goto :slot_menu
    )
    if /i not "!CONFIRM!"=="Да" (
        echo Неверный ввод. Введите Да или Нет.
        pause
        goto :slot_menu
    )
)
curl -s !CFG_URL! > "%TARGET_DIR%\%FILE%"
if !errorlevel! neq 0 (
    echo Ошибка при создании файла %FILE%.
    pause
    goto :slot_menu
)
echo Конфигурация для слота %SLOT% успешно обработана.
pause
goto :main_menu
